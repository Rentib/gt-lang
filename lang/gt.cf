entrypoints     TranslationUnit ;

Program.        TranslationUnit ::= [Decl] ;

TInt.           Type ::= "int" ;
TBool.          Type ::= "bool" ;
TChar.          Type ::= "char" ;
TVoid.          Type ::= "void" ;
TConst.         Type ::= "const" Type ;
TFunc.          Type ::= "(" [Type] ")" "->" Type ;
TArray.         Type ::= "[" Type "]" ;
separator       Type  "," ;

PArgVal.        Arg ::= Ident ":" Type ;
PArgRef.        Arg ::= "&" Ident ":" Type ;
separator       Arg "," ;

PBlock.         Block ::= "{" [Decl] [Instr] "}" ;

DVar.           Decl ::= "var"   [DItem] ";" ;
DConst.         Decl ::= "const" [DItemConst] ";" ;
DFunc.          Decl ::= "fn" Ident "(" [Arg] ")" "->" Type Block ;
separator       Decl "" ;

DItemNoInit.    DItem ::= Ident ":" Type ;
DItemInit.      DItem ::= Ident "=" Expr1 ;
separator       nonempty DItem "," ;
DItemConstInit. DItemConst ::= Ident "=" Expr1 ;
separator       nonempty DItemConst "," ;

IBlock.         Instr ::= Block ;
IExpr.          Instr ::= Expr ";" ;
IIf.            Instr ::= "if" "(" Expr1 ")" Instr ;
IIfElse.        Instr ::= "if" "(" Expr1 ")" Instr "else" Instr ;
IWhile.         Instr ::= "while" "(" Expr1 ")" Instr ;
IFor.           Instr ::= "for" "(" Expr ";" Expr1 ";" Expr ")" Instr ;
IContinue.      Instr ::= "continue" ";" ;
IBreak.         Instr ::= "break" ";" ;
IReturn.        Instr ::= "return" Expr ";" ;
separator       Instr "" ;

coercions       Expr 9 ;
ELitInt.        Expr9 ::= Integer ;
ELitChar.       Expr9 ::= Char ;
ELitString.     Expr9 ::= String ;
ELitTrue.       Expr9 ::= "true" ;
ELitFalse.      Expr9 ::= "false" ;
EIdent.         Expr9 ::= Ident ;
EIndex.         Expr9 ::= Expr9 "[" Expr1 "]" ;
EApply.         Expr9 ::= Expr9 "(" [Expr1] ")" ;
ECast.          Expr8 ::= "(" Type ")" Expr9 ;
EUOp.           Expr8 ::= UnaryOp Expr9 ;
EMul.           Expr7 ::= Expr7 MulOp Expr8 ;
EAdd.           Expr6 ::= Expr6 AddOp Expr7 ;
ERel.           Expr5 ::= Expr5 RelOp Expr6 ;
EEq.            Expr4 ::= Expr4 EqOp Expr5 ;
EAnd.           Expr3 ::= Expr3 "&&" Expr4 ;
EOr.            Expr2 ::= Expr2 "||" Expr3 ;
EAssign.        Expr1 ::= Expr9 AssignOp Expr2 ;
ELambda.        Expr1 ::= "\\" "(" [Arg] ")" "->" Type "=>" Block ;
EEmpty.         Expr  ::= ;
separator       Expr1 "," ;

OpUnaryPlus.    UnaryOp ::= "+" ;
OpUnaryMinus.   UnaryOp ::= "-" ;
OpUnaryBang.    UnaryOp ::= "!" ;
OpUnaryInc.     UnaryOp ::= "++" ;
OpUnaryDec.     UnaryOp ::= "--" ;

OpTimes.        MulOp ::= "*" ;
OpDiv.          MulOp ::= "/" ;
OpMod.          MulOp ::= "%" ;

OpPlus.         AddOp ::= "+" ;
OpMinus.        AddOp ::= "-" ;

OpLT.           RelOp ::= "<" ;
OpLE.           RelOp ::= "<=" ;
OpGT.           RelOp ::= ">" ;
OpGE.           RelOp ::= ">=" ;

OpEq.           EqOp ::= "==" ;
OpNeq.          EqOp ::= "!=" ;

OpAssign.       AssignOp ::=  "=" ;
OpAssignTimes.  AssignOp ::= "*=" ;
OpAssignDiv.    AssignOp ::= "/=" ;
OpAssignMod.    AssignOp ::= "%=" ;
OpAssignPlus.   AssignOp ::= "+=" ;
OpAssignMinus.  AssignOp ::= "-=" ;

comment         "//" ;
comment         "/*" "*/" ;
